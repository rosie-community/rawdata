-- -*- Mode: rpl; -*-                                                                                   
--
-- destructure.rpl
--
-- Â© Copyright Jamie A. Jennings 2018.
-- LICENSE: MIT License (https://opensource.org/licenses/mit-license.html)
-- AUTHOR: Jamie A. Jennings

package destructure

term=$			    -- termination pattern (could be anything)
rest={!term .}*

local dash = "-"
local slash = "/"
local comma = ","
local semicolon = ";"

dashes = {{keepto:>{term / dash} dash}+ rest}
slashes = {{keepto:>{term / slash} slash}+ rest}
commas = {{keepto:>{term / comma} comma}+ rest}
semicolons = {{keepto:>{term / semicolon} semicolon}+ rest}

-- test dashes accepts "-", "--", "-l", "i--"
-- test dashes rejects "", "abcdef"

-- test dashes accepts "12-13-14-dog-cat", "en-US", "en-CA", "en-GB"
-- test slashes accepts "3/25/2018", "4/5", "XX/XY", "/r"
-- test commas accepts "Detroit, MI, U.S."
-- test semicolons accepts "145;32;NJ", ";", "48K;"

local alpha = [:alpha:]+ 
local num = [:digit:]+
alphanum = { {alpha num}+ alpha? }
numalpha = { {num alpha}+ num? }

-- test alphanum accepts "A3", "ABC0", "BLDG501", "WD40", "A2N1"
-- test alphanum rejects "", "A", "123ABC"
-- test numalpha accepts "3A", "01ABC", "123ABC", "501C", "1B12"
-- test numalpha rejects "", "A", "6", "A3"

alias tryall = dashes / slashes / commas / semicolons / alphanum / numalpha

-- test tryall accepts "123DEF", "A10", "1B12"
-- test tryall includes dashes "WD-40"
-- test tryall excludes alphanum "WD-40"

